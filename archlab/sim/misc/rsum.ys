#Execution begins at address 0,written by peanwang仿照csapp p254编写
	.pos 0
	irmovq stack,%rsp
	call main
	halt
# Sample linked list从实验说明获取测试数据
	.align 8
ele1:
	.quad 0x00a
	.quad ele2
ele2:
	.quad 0x0b0
	.quad ele3
ele3:
	.quad 0xc00
	.quad 0
#This is main function ele1为链表头作第一个参数
main:
	irmovq ele1,%rdi
	call rsum_list
	ret 
#long sum_list(list_ptr ls)

rsum_list:
        pushq %r14
        irmovq $0, %rax
        andq %rdi,%rdi
        je return
        mrmovq 0(%rdi), %r14 #store the node value in %rsi
        mrmovq 8(%rdi), %rdi #replace %rdi with the node pointer
        call rsum_list
        addq %r14, %rax

return:
        popq %r14
        ret
                

#stack starts here and grows to lower addresses
	.pos 0x200
stack:
